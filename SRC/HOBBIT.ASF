;***************************
; THE HOBBIT (DISK VERSION)
;***************************
;
VDP EQU 98H
BEEP EQU 00C0H
CHGMOD EQU 005FH
CHPUT EQU 00A2H
START@ EQU 0FDA4H
;
 FILE HOB-TITL
 FILE HOB-TEND
 FILE HOB-LOAD
 FILE FILE-IN
;
 NAME HOBBIT
;
 ORG 0C000H
;
;
START LD (ERROR+1),SP
 CALL SCRSET
 CALL HTLOAD
 CALL HBTITL
 CALL HBLOAD
 CALL HBTEND
;
 DI
 CALL ALLRAM
;
 LD HL,GO_
 LD DE,START@
 LD BC,SCRSET-GO_
 LDIR
;
 JP START@
;
;
GO_ LD A,4
 OUT (VDP+1),A
 LD A,14+80H
 OUT (VDP+1),A
 XOR A
 OUT (VDP+1),A
 OUT (VDP+1),A
 LD HL,0000H
 LD A,0F0H
 LD BC,VDP
L_GO_ INIR
 DEC A
 JR NZ,L_GO_
;
 XOR A
 OUT (VDP+1),A
 LD A,14+80H
 OUT (VDP+1),A
;
 XOR A
 OUT (VDP+1),A
 LD A,17+80H
 OUT (VDP+1),A
 LD BC,VDP+803H
 LD HL,VDPDAT-GO_+START@
 OTIR
;
 LD SP,0FFFEH
 JP 8620H
;
VDPDAT DB 02H,0C0H,06H,0FFH,03H,3EH,00H,0FH
;
SCRSET LD HL,COLORS
 LD DE,0F3E9H ;COLOR FORGROUND
 LD BC,3
 LDIR
 LD A,4
 CALL CHGMOD
;
 XOR A
 OUT (VDP+1),A
 LD A,16+80H
 OUT (VDP+1),A
 LD HL,PALETT
 LD BC,VDP+2002H
 OTIR
;
 LD HL,2000H
 LD A,0EFH
 CALL FILVRM
;
 RET
COLORS DB 1,15,15
PALETT DB 0H,0H,0H,0H,11H,6H,33H,7H,17H,1H,27H,3H,51H,1H,27H,6H,71H,1H,73H,3H,61H,6H,64H,6H,11H,4H,65H,2H,55H,5H,77H,7H
;
FILVRM PUSH AF
 CALL SETWR0
 LD BC,1800H
 POP AF
L_FVRM OUT (VDP),A
 DEC C
 JP NZ,L_FVRM
 DJNZ L_FVRM
 RET
;
HTLOAD LD DE,9000H
 CALL F_DMA
;
 LD HL,FILE_0
 CALL F_OPEN
 JP NZ,ERROR
;
 LD HL,3000H
 CALL F_READ
;
 RET
FILE_0 DM 'HOBBIT  DAT'
;
ERROR LD SP,0000 ;?
 EI
 CALL BEEP
 LD A,(0FCAFH) ;SCREEN NO.
 CALL CHGMOD
 LD HL,MERROR
 CALL PUTMES
 RET
MERROR DM 'FILE READ ERROR !!'
 DB 0AH,0DH,'$'
;
;
PUTMES LD A,(HL)
 CP '$'
 RET Z
 CALL CHPUT
 JP PUTMES
;
; Cy= bit 16
; C= 40H:WRITE / 00H:READ
VSET LD A,H
 RLA
 RLA
 RLA
 AND 7
 OUT (VDP+1),A
 LD A,14+80H
 OUT (VDP+1),A
 LD A,L
 OUT (VDP+1),A
 LD A,H
 AND 3FH
 OR C
 OUT (VDP+1),A
 RET
;
;
SETWR0 OR A
 LD C,40H
 JP VSET
;
SETWR1 SCF
 LD C,40H
 JP VSET
;
;
ALLRAM IN A,(0A8H)
 CALL ALRAM@
 OUT (0A8H),A
 LD A,(0FFFFH)
 CPL
 CALL ALRAM@
 LD (0FFFFH),A
 RET
ALRAM@ RLCA
 RLCA
 AND 3
 LD B,A
 RLCA
 RLCA
 OR B
 RLCA
 RLCA
 OR B
 RLCA
 RLCA
 OR B
 RET
;
