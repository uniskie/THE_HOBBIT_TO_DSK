;*********************************
;     TIMER & KEY  INTERRUPT
; RAM C000-FEFF > VRAM 8000-BEFF
;*********************************
;       NAME  RAMG-EXP
;
PSGIOA EQU 0A0H
PSGIOW EQU 0A1H
PSGIOR EQU 0A2H
PSG_SV EQU 0FC4CH
;
STACK EQU 0F378H ;DISK WORK AREA HEAD
SAVSP EQU 0FAFAH
CHGCAP EQU 00132H
;
V_PG1 EQU 2
M_SIZE EQU 1EH
M_ADR EQU 0E000H
;
;
EX_TRAP DI
 IN A,(KEYIO+1)
 AND 0F0H
 OR 7
 OUT (KEYIO+1),A
 IN A,(KEYIO)
 AND 4 ;ESC
TRAP_MS JP NZ,0000H ;?
;
 LD (SVW_SP),SP
 LD SP,SPW
;
 LD HL,PSG_SV
 LD BC,PSGIOR
 XOR A
L_PSGSV OUT (PSGIOA),A
 INI
 INC A
 CP 13
 JP NZ,L_PSGSV
 LD A,7
 OUT (PSGIOA),A
 LD A,0BFH
 OUT (PSGIOW),A
;
 XOR A
 CALL CHGCAP
;
 LD DE,0704H ;ESC
L_ESC CALL KEYIN
 JP Z,L_ESC
S_CAP LD DE,0
L_FUNC LD HL,FC_TBL
L_FUNC_ LD A,D
 AND 8
 LD A,1
 JP NZ,J_CAP
 XOR A
J_CAP CALL CHGCAP
 INC DE
 PUSH DE
;
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 LD C,(HL)
 INC HL
 LD B,(HL)
 INC HL
 CALL KEYIN
 POP DE
 JP NZ,L_FUNC_
 XOR A
 CALL CHGCAP
 LD H,B
 LD L,C
 JP (HL)
;
RETURN@ JP S_CAP
W_CAP DB 0
;
RETURN LD DE,0704H ;ESC
L_RET CALL KEYIN
 JP Z,L_RET
;
 LD HL,PSG_SV
 LD BC,PSGIOW
 XOR A
L_PSGLD OUT (PSGIOA),A
 OUTI
 INC A
 CP 13
 JP NZ,L_PSGLD
;
 CALL CHGCAP
 LD SP,(SVW_SP)
 LD A,1
 OR A
 JP TRAP_MS
;
;
FC_TBL DW 0602H,CTRL ; CTRL
 DW 0501H,SAVE ;'S'
 DW 0402H,LOAD ;'L'
 DW 0704H,RETURN ; ESC
 DW 0,L_FUNC ; END of DATA
;
CTRL LD HL,FC_TBL@
 JP L_FUNC_
FC_TBL@ DW 0120H,QUIT ;'@'
 DW 0710H,RESET ; STOP
 DW 0,L_FUNC ; END of DATA
;
;
QUIT JP RET_BAS ;COMMON
;
SAVE LD A,V_PG1
 LD HL,RETURN@
 PUSH HL
SAVE_ INC A
 LD (SAVE__+1),A
 DEC A
 LD HL,M_ADR
 LD B,M_SIZE
 LD C,20H+40H
 CALL TF_VRM
;
 LD D,0
L_SAVE LD C,D
 LD HL,BUF
 LD B,1
 LD A,0
 CALL TF_RAM
 LD A,40H
 OR D
 LD C,A
 LD HL,BUF
 LD B,1
SAVE__ LD A,0 ;vram page +1
 CALL TF_VRM
;
 INC D
 LD A,40H
 SUB A,D
 JP NZ,L_SAVE
;
 RET
;
LOAD LD A,V_PG1
 LD HL,RETURN@
 PUSH HL
LOAD_ LD D,0
 LD (LOAD__+1),A
 INC A
 LD (L_LOAD+1),A
L_LOAD LD A,0 ;vram page +1
 LD HL,BUF
 LD B,1
 LD C,D
 CALL TF_RAM
 LD A,40H
 OR D
 LD C,A
 LD HL,BUF
 LD B,1
 LD A,0
 CALL TF_VRM
;
 INC D
 LD A,40H
 SUB A,D
 JP NZ,L_LOAD
;
 LD HL,M_ADR
 LD B,M_SIZE
 LD C,20H
LOAD__ LD A,0 ;vram page
 CALL TF_RAM
 CALL WR_CRG
 RET
;
;
RESET INC A
 CALL CHGCAP
;
 LD HL,(4002H)
 JP (HL)
;
;
SVW_SP EQU SAVSP
