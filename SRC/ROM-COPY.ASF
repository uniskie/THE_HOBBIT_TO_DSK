;**** ROM COPY & WRITE ***
;*    WITH DISK BASIC    *
;*************************
;
;*********************
;* DATA:VRAM 10000H~ *
;* SIZE:+4000H       *
;*********************
;
; CLEAR nn,&HCFFF
; DEFINT D:DIM D(3)
;
; DEF USR=&HD000
;
; D(0)=ROM SLOT No.
; D(1)=ROM ADDRESS
; D(2)=VRAM ADDRESS
; D=USR(VARPTR(D(0)))
;
; DEF USR=&HD003
;
; D(0)=ROM SLOT No.
; D(1)=ROM ADDRESS
; D(2)=WRITTING DATA
; D=USR(VARPTR(D(0)))
;
; DEF USR=&HD006
;
; D(0)=ROM SLOT No.
; D(1)=ROM ADDRESS
; D=USR(VARPTR(D(0)))
; (D=2byte DATA)
;
 NAME ROM-COPY
;
;
RAMAD0 EQU 0F341H
EXPTBL EQU 0FCC1H
ENASLT EQU 0024H
WRSLT EQU 0014H
RDSLT EQU 000CH
;
 ORG 0D000H
;
;*** JUMP TABLE
 JP ROMCPY ; ROM COPY
 JP BNKSEL ; BANK SELECT (WRITE ROM)
 JP RDWORD ; 2byte READ (READ ROM)
;
GETDAT CP 2
 JP Z,GTDAT_
 POP AF
 RET
GTDAT_ INC HL
 INC HL
 LD (HEN_ADR),HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 EX DE,HL
;
 LD DE,DAT_BUF
 LD BC,6
 LDIR
 DI
 POP HL
 LD (RET_SP+1),SP
 LD SP,(0F378H)
 JP (HL)
;
DAT_BUF DS 6
HEN_ADR DW 0
;
ROMCPY CALL GETDAT
 LD HL,(DAT_BUF+2)
 LD A,(DAT_BUF)
 CALL ENASLT
;
 LD A,(0006H+1)
 LD C,A
 INC C
 LD A,(DAT_BUF+5)
 SCF
 RLA
 RLA
 RLA
 AND 7
 OUT (C),A
 LD A,14+80H
 OUT (C),A
 LD A,(DAT_BUF+4)
 OUT (C),A
 LD A,(DAT_BUF+5)
 AND 3FH
 OR 40H
 OUT (C),A
;
 LD HL,(DAT_BUF+2)
 LD B,0
 DEC C
 LD A,40H
L_ROMC OTIR
 DEC A
 JP NZ,L_ROMC
;
 LD A,(DAT_BUF+3)
 LD DE,(EXPTBL)
 CP 80H
 JP C,$+3+4
 LD DE,(RAMAD0+2)
 LD H,A
 LD A,E
 CALL ENASLT
;
RET_SP LD SP,0 ;??
 EI
 RET
;
;
BNKSEL CALL GETDAT
 LD HL,(DAT_BUF+2)
 LD DE,(DAT_BUF+4)
 LD A,(DAT_BUF)
 CALL WRSLT
;
 JP RET_SP
;
RDWORD CALL GETDAT
 LD HL,(DAT_BUF+2)
 LD A,(DAT_BUF)
 CALL RDSLT
 LD (RDWR2_+1),A
 LD A,(DAT_BUF)
 INC HL
 CALL RDSLT
 LD HL,(HEN_ADR)
 INC HL
 LD (HL),A
 DEC HL
RDWR2_ LD (HL),0 ;??
;
 JP RET_SP
;
 DS 16
STACK DW 0
